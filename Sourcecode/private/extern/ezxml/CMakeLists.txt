include(${CMAKE_CURRENT_SOURCE_DIR}/CMakeLists.conan.txt)
cmake_minimum_required(VERSION 3.7.2)
project(ezxml
        VERSION 0.0.0
        LANGUAGES CXX)

option(EZXML_BUILD_TESTS "build or skip the test suite" OFF)
option(EZXML_BUILD_EXAMPLES "build or skip the examples" OFF)

include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

find_package( Threads )
find_package(pugixml CONFIG REQUIRED)

set(SOURCE_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/src")
set(PRIVATE_DIR "${SOURCE_ROOT}/private")
set(PUBLIC_DIR "${SOURCE_ROOT}/include")
set(TEST_DIR "${SOURCE_ROOT}/test")

set(CMAKE_MODULE_PATH ${CMAKE_BINARY_DIR} ${CMAKE_MODULE_PATH})
set(CMAKE_PREFIX_PATH ${CMAKE_BINARY_DIR} ${CMAKE_PREFIX_PATH})

file(GLOB_RECURSE PUBLIC_HEADERS ${PUBLIC_DIR}/ezxml/*.h)
file(GLOB_RECURSE PRIVATE_HEADERS ${PRIVATE_DIR}/ezx/*.h)
file(GLOB_RECURSE PRIVATE_CXX ${PRIVATE_DIR}/private/*.cpp)

add_library(ezxml STATIC ${PRIVATE_CXX})
target_link_libraries(ezxml PRIVATE pugixml::pugixml)

source_group( "ezxml-public" FILES ${PUBLIC_HEADERS})
source_group( "ezxml-private" FILES ${PRIVATE_HEADERS} ${PRIVATE_CXX})
set_property(TARGET ezxml PROPERTY CXX_STANDARD 14)
# target_include_directories(ezxml PUBLIC ${PUBLIC_DIR})
target_include_directories(ezxml PRIVATE ${PRIVATE_DIR})
target_include_directories(${PROJECT_NAME} PUBLIC
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
    $<BUILD_INTERFACE:${PUBLIC_DIR}>
)

# Install Package Configuration
install(TARGETS ${PROJECT_NAME}
    EXPORT ${PROJECT_NAME}_targets
    ARCHIVE DESTINATION lib
    LIBRARY DESTINATION lib
)
install(EXPORT ${PROJECT_NAME}_targets
    NAMESPACE ${PROJECT_NAME}::
    FILE ${PROJECT_NAME}-config.cmake
    DESTINATION "${CMAKE_INSTALL_DATADIR}/cmake/${PROJECT_NAME}"
)
install(DIRECTORY "${PUBLIC_DIR}/ezxml" DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")

file(WRITE ${TEST_DIR}/Path.h
    "// ezxml, Copyright 2019 by Matthew James Briggs
    // This file is auto generated by CMake
    #pragma once

    // The absolute path to the root of the repository.
    #ifndef EZXML_ROOT
    #define EZXML_ROOT \"${CMAKE_CURRENT_SOURCE_DIR}\"
    #endif

    // The absolute path to the binary output directory.
    #ifndef EZXML_BIN
    #define EZXML_BIN \"${CMAKE_BINARY_DIR}\"
    #endif"
)

if(EZXML_BUILD_TESTS)
    message(STATUS "${PROJECT_NAME}:${CMAKE_CURRENT_LIST_LINE} tests will be compiled")
    target_compile_options(ezxml PRIVATE -Werror -Wall -Wextra)
    file(GLOB_RECURSE TEST_CXX ${TEST_DIR}/*.cpp)
    file(GLOB_RECURSE TEST_HEADERS ${TEST_CXX}/*.h)
    source_group( "ezxml-test" FILES ${TEST_CXX} ${TEST_HEADERS})
    add_executable(ezxml_test ${TEST_CXX} ${TEST_HEADERS})
    target_include_directories(ezxml_test PRIVATE ${PRIVATE_DIR})
    target_include_directories(ezxml_test PRIVATE ${PUBLIC_DIR})
    target_include_directories(ezxml_test PRIVATE ${PRIVATE_DIR})
    target_link_libraries(ezxml_test ezxml)
    target_link_libraries(ezxml_test ${CMAKE_THREAD_LIBS_INIT})
    set_property(TARGET ezxml_test PROPERTY CXX_STANDARD 14)
else()
    message(STATUS "${PROJECT_NAME}:${CMAKE_CURRENT_LIST_LINE} tests will NOT be compiled")
endif()